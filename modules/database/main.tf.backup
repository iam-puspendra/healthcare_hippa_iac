resource "aws_docdb_cluster" "cluster" {
  cluster_identifier      = "hipaa-docdb-cluster"
  engine                  = "docdb"
  master_username         = var.db_username
  master_password         = var.db_password
  storage_encrypted       = true
  kms_key_id              = var.kms_key_id
  db_subnet_group_name    = aws_docdb_subnet_group.docdb_subnet_group.name
  backup_retention_period = 7
  skip_final_snapshot     = true
  vpc_security_group_ids  = [aws_security_group.docdb.id]
  tags = {
    Name = "hipaa-docdb-cluster"
  }
}

resource "aws_docdb_subnet_group" "docdb_subnet_group" {
  name       = "hipaa-docdb-subnet-group"
  subnet_ids = var.private_db_subnet_ids
}

resource "aws_docdb_cluster_instance" "instance" {
  count              = var.instance_count
  cluster_identifier = aws_docdb_cluster.cluster.id
  instance_class     = "db.t4g.medium"
  identifier         = "hipaa-docdb-instance-${count.index}"
}

# DocumentDB Security Group
resource "aws_security_group" "docdb" {
  name        = "hipaa-docdb-sg"
  description = "Security group for DocumentDB cluster"
  vpc_id      = var.vpc_id

  ingress {
    from_port       = 27017
    to_port         = 27017
    protocol        = "tcp"
    security_groups = [var.app_security_group_id]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "hipaa-docdb-sg"
  }
}

